
\newpage
\subsection{Lemmas in Second Refinement}

\begin{alltt}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Refinement2 :                                %
%   Applies the ``Refinement'' theory to yield %
%   the second refinement lemma ``R2''.        %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Refinement2[
  NODES : posnat, 
  SONS  : posnat, 
  ROOTS : posnat] : THEORY

BEGIN

  ASSUMING
    roots_within : ASSUMPTION ROOTS <= NODES
  ENDASSUMING

  I1 : THEORY = Garbage_Collector1[NODES,SONS,ROOTS]
  I2 : THEORY = Garbage_Collector2[NODES,SONS,ROOTS]
  IMPORTING Garbage_Collector2_Inv[NODES,SONS,ROOTS]

  s     : VAR I2.State
  r1,r2 : VAR (I)
  n,k   : VAR Node
  i     : VAR Index
  cs    : VAR Colours

  abs(s):I1.State = 
    (# MU := CASES MU(s) OF
               MUTATE : MUTATE,
               COLOUR : COLOUR
             ENDCASES,
       CHI := CASES CHI(s) OF
                COLOUR_ROOTS : COLOUR,
                   PROPAGATE : COLOUR,
                      TEST_L : TEST_L,
                      APPEND : APPEND
              ENDCASES,
       Q   := Q(s),
       L   := L(s),
       C   := C(s),
       M   := M(s)
     #)

  sim_mutate : LEMMA
    (EXISTS n,i,k: r2 = Rule_mutate(n,i,k)(r1)) IMPLIES
      EXISTS n,i,k: abs(r2) = Rule_mutate(n,i,k)(abs(r1))

  sim_colour_target : LEMMA
    r2 = Rule_colour_target(r1) IMPLIES
      abs(r2) = Rule_colour_target(abs(r1))

  sim_stop_colouring_roots : LEMMA
    r2 = Rule_stop_colouring_roots(r1) IMPLIES
      abs(r2) = abs(r1)

  sim_colour_root : LEMMA
    r2 = Rule_colour_root(r1) IMPLIES
      (EXISTS n: abs(r2) = Rule_colour(n)(abs(r1))) OR abs(r2) = abs(r1)

  sim_stop_propagating : LEMMA
    r2 = Rule_stop_propagating(r1) IMPLIES
      abs(r2) = Rule_stop_colouring(abs(r1)) OR abs(r2) = abs(r1)

  sim_propagate : LEMMA
    (EXISTS n,i: r2 = Rule_propagate(n,i)(r1)) IMPLIES
      (EXISTS k: abs(r2) = Rule_colour(k)(abs(r1))) OR
      abs(r2) = abs(r1)

  sim_stop_appending : LEMMA
    r2 = Rule_stop_appending(r1) IMPLIES
      abs(r2) = Rule_stop_appending(abs(r1))

  sim_continue_appending : LEMMA
    r2 = Rule_continue_appending(r1) IMPLIES
      abs(r2) = Rule_continue_appending(abs(r1))

  sim_black_to_white : LEMMA
    r2 = Rule_black_to_white(r1) IMPLIES
      abs(r2) = Rule_black_to_white(abs(r1))

  sim_append_white : LEMMA
    r2 = Rule_append_white(r1) IMPLIES
      abs(r2) = Rule_append_white(abs(r1))

  next_h : LEMMA 
    next(r1,r2) IMPLIES next(abs(r1),abs(r2))

  R2 : THEORY = 
    Refinement[I1.O_State,
      I1.State,I1.init,I1.next,I1.proj,
      I2.State,I2.init,I2.next,I2.proj,
      abs,I]      

END Refinement2
\end{alltt}


\newpage
\begin{alltt}
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Garbage_Collector2_Inv :                                        %
%   Defines all invariants used in proving the second refinement. %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Garbage_Collector2_Inv[
  NODES : posnat, 
  SONS  : posnat, 
  ROOTS : posnat] : THEORY

BEGIN

  ASSUMING
    roots_within : ASSUMPTION ROOTS <= NODES
  ENDASSUMING

  IMPORTING Memory_Properties[NODES,SONS,ROOTS]
  IMPORTING Garbage_Collector2[NODES,SONS,ROOTS]
  IMPORTING Invariant_Predicates[State]

  s : VAR State

  inv1(s):bool =
    CHI(s)=PROPAGATE IMPLIES black_roots(ROOTS)(C(s))

  inv2(s):bool =
    CHI(s)=COLOUR_ROOTS IMPLIES black_roots(K(s))(C(s))

  I : pred[State] = inv1 & inv2

  pi : pred[pred[State]] = preserved(init,next)(I)

  i_inv1 : LEMMA I IMPLIES inv1
  i_inv2 : LEMMA I IMPLIES inv2

  p_inv1 : LEMMA pi(inv1)
  p_inv2 : LEMMA pi(inv2)

  p_I : LEMMA pi(I)

  inv : LEMMA invariant(init,next)(I)

END Garbage_Collector2_Inv
\end{alltt}


%%% Local Variables: 
%%% mode: latex
%%% TeX-master: t
%%% End: 
